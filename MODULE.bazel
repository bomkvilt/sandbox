"""
TODO: add something to manage dependency versions.
HINT: from `hedron_compile_commands`:
    Replace the commit hash (above) with the latest (https://github.com/hedronvision/bazel-compile-commands-extractor/commits/main).
    Even better, set up Renovate and let it do the work for you (see "Suggestion: Updates" in the README).
"""

module(name="sandbox")

# ----------------------------------------------------------
# setup toolchain
# ----------------------------------------------------------

bazel_dep(name="toolchains_llvm", version="1.1.2")

llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm", dev_dependency=True)
llvm.toolchain(
    name="llvm_toolchain",
    llvm_versions={"": "17.0.6"},
    cxx_standard={"": "c++23"},
)

use_repo(llvm, "llvm_toolchain", "llvm_toolchain_llvm")
register_toolchains("@llvm_toolchain//:all")


# ----------------------------------------------------------
# support of rust targets
# ----------------------------------------------------------

bazel_dep(name="rules_rust", version="0.51.0")
git_override(
    module_name="rules_rust",
    remote="https://github.com/bazelbuild/rules_rust.git",
    commit="144d34fdaa3c3c5fe27a9666b760aa31aa9155a5",
)

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(edition="2021", versions=["1.81.0"])
use_repo(rust, "rust_toolchains")

# SA: https://bazelbuild.github.io/rules_rust/crate_universe_bzlmod.html
crate = use_extension("@rules_rust//crate_universe:extension.bzl", "crate")
crate.from_cargo(
    name = "crates",
    cargo_lockfile = "//:Cargo.lock",
    manifests = [
        "//:Cargo.toml",
        "//rust/basics/00.hellow_world:Cargo.toml"
    ],
)
use_repo(crate, "crates")


# ----------------------------------------------------------
# setup external c++ packages from Conan
# ----------------------------------------------------------

load_conan_dependencies = use_extension(
    "//build/conan:conan_deps_module_extension.bzl",
    "conan_extension"
)
use_repo(load_conan_dependencies, "asio", "gtest", "benchmark")


# ----------------------------------------------------------
# extract build commmands (clangd & co.)
# ----------------------------------------------------------

bazel_dep(name="hedron_compile_commands", dev_dependency=True)
git_override(
    module_name="hedron_compile_commands",
    remote="https://github.com/hedronvision/bazel-compile-commands-extractor.git",
    commit="1e08f8e0507b6b6b1f4416a9a22cf5c28beaba93",
)
